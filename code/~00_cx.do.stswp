/*==============================================================================
Project    : School Spending – F-33 and INDFIN Integration
File       : 00_cx.do
Purpose    : Construct unified district panels and crosswalks linking NCES F-33, INDFIN, 
              and GRF district identifiers.
Author     : Myles Owens
Institution: Hoover Institution, Stanford University
Date       : 2025-10-27
-------------------------------------------------------------------------------
Description:
1. Build and clean F-33 and INDFIN district by year panels.
2. Generate canonical crosswalks between LEAID, GOVID, and GRF IDs.
3. Produce merged datasets for downstream event-study and panel analysis.

Global Path:
    global SchoolSpending "C:\Users\maowens\OneDrive - Stanford\school\git"

Inputs:
    - $SchoolSpending\data\raw\nces\build_f33_in_dir\*.sas7bdat
    - $SchoolSpending\data\raw\indfin\build_indfin_in_dir\
    - $SchoolSpending\data\raw\GRF69\DS0001\03515-0001-Data.txt
    - $SchoolSpending\data\ALLids.csv

Outputs:
    - f33_panel.dta
    - indfin_panel.dta
    - canon_crosswalk.dta
    - f33_indfin_grf_canon.dta

Notes:
    - Flags anomalous values but retains them for traceability.
    - Canonical ID logic prioritizes valid, non-junk identifiers.
    - Ensure `$SchoolSpending` global is correctly defined before execution.
==============================================================================*/

*/


*** ---------------------------------------------------------------------------
*** 1. Build f33 panel (NCES Data)
*** ---------------------------------------------------------------------------


clear
set more off
cd "$SchoolSpending/data/raw/nces/build_f33_in_dir"

* Identify all F-33 raw SAS files
local files : dir "." files "*.sas7bdat"

* Loop through and convert each SAS file to Stata format
foreach f of local files {
    disp "Processing `f'"
    
    import sas using "`f'", clear

    local outname = subinstr("`f'", ".sas7bdat", ".dta", .)
    save "`outname'", replace
}

*** Append all F-33 files into one panel
local files : dir "." files "*.dta"
tempfile base

local first = 1
foreach f of local files {
    disp "Processing `f'"

    * Try to pull year (example: "sdf92.dta" → 1992)
    local shortyr = substr("`f'", 4, 2)
    local year = cond(real("`shortyr'") < 50, 2000 + real("`shortyr'"), 1900 + real("`shortyr'"))

    use LEAID CENSUSID NAME V33 TOTALEXP using "`f'", clear
    gen year = `year'

    if `first' {
        save `base'
        local first = 0
    }
    else {
        append using `base'
        save `base', replace
    }
}

use `base', clear


cd "$SchoolSpending\data"

* Flag anomalies (keep for later inspection)
gen bad_pop   = (V33 < 0)        // zero or negative pop
label var bad_pop "Zero or negative pop"
label var bad_pop "Flag: zero or negative population"
gen bad_exp   = (TOTALEXP < 0)    // negative expenditure
label var bad_exp "Flag: negative expenditure"
drop if bad_exp ==1
drop if bad_pop ==1


* Calculate per-pupil expenditure (in #1000s)
gen pp_exp = .
replace pp_exp = (TOTALEXP/1000) / V33

label var pp_exp "Per-pupil expenditure"
drop if year < 1992 | year > 2019
* Extract 9-digit GOVID from 14-digit CENSUSID
gen str9 GOVID = substr(CENSUSID,1,9)
save f33_panel, replace




*** ---------------------------------------------------------------------------
*** 2. Build indifn panel (Census Individual Finance)
*** ---------------------------------------------------------------------------

***Settings

local years 67 70 71 72 73 74 75 76 77 78 79 ///
            80 81 82 83 84 85 86 87 88 89   ///
            90 91   // 68 & 69 skipped

local inDir  "$SchoolSpending/data/raw/indfin/build_indfin_in_dir"
local outDir "$SchoolSpending/data/raw/indfin/build_indfin_out_dir"

*Variables to retain
local keepvars ///
    sortcode year4 id idchanged statecode typecode county name ///
    population elemeductotalexp totalexpenditure totaleductotalexp
	
* Clean and standardize each yearly INDFIN dataset
foreach y of local years {
    di as txt "→ trimming `y'"
    use "`inDir'/indfin`y'a.dta", clear
    keep if typecode == 5                     // focus on school districts
    keep `keepvars'
    save "`outDir'/f`y'.dta", replace
}

* Stack all cleaned years into one panel
local first : word 1 of `years'
use "`outDir'/f`first'.dta", clear

foreach y of local years {
    if "`y'" == "`first'" continue
    append using "`outDir'/f`y'.dta"
}

save "`outDir'/indfin_panel_1967_1991_clean.dta", replace
di as result "✓ INDFIN panel (1967‑1991) complete."

* Create standard 9-digit GOVID
gen str9 GOVID = string(id, "%09.0f")
cd "$SchoolSpending\data"

gen pp_exp = .
replace pp_exp = totalexpenditure/population
label var pp_exp "Per-pupil expenditure"
save indfin_panel, replace



/*
*** ---------------------------------------------------------------------------
*** 3. Import Master ID Files (F33 IDs, INDFIN IDs, GRF)
*** ---------------------------------------------------------------------------

*** --- F33 ID crosswalk
clear
set more off
cd "$SchoolSpending\data\raw\nces\build_f33_in_dir"

local files : dir "." files "*.sas7bdat"

foreach f of local files {
    disp "Processing `f'"
    
    import sas using "`f'", clear

    local outname = subinstr("`f'", ".sas7bdat", ".dta", .)
    save "`outname'", replace
}

* Append all yearly ID files
local files : dir "." files "*.dta"
tempfile base

local first = 1
foreach f of local files {
    disp "Processing `f'"

    * Try to pull year (example: "sdf92.dta" → 1992)
    local shortyr = substr("`f'", 4, 2)
    local year = cond(real("`shortyr'") < 50, 2000 + real("`shortyr'"), 1900 + real("`shortyr'"))

    use LEAID CENSUSID NAME using "`f'", clear
    gen year = `year'

    if `first' {
        save `base'
        local first = 0
    }
    else {
        append using `base'
        save `base', replace
    }
}

use `base', clear


*Data
cd "$SchoolSpending\data"
gen str9 GOVID = substr(CENSUSID,1,9)
save f33_id, replace


*** --- INDFIN Master ID list
clear
set more off
cd "$SchoolSpending\data"
import delimited using "$SchoolSpending\data\raw\indfin\ALLids.csv"
gen str9 GOVID = string(id, "%09.0f")
keep if typecode == 5  // school districts only
drop id idchanged typecode county censusreg statecode ///
version population jacketunit zerodata
save indfin_id, replace
*/


*** --- GRF (Geographic Reference File) 1969
clear 
set more off
local dfile "$SchoolSpending\data\raw\GRF69\DS0001\03515-0001-Data.txt"

*** import fixed-width ASCII [ICPSR layout]
infix ///
    /* numeric codes */                                                        ///
    byte  stc70  1-2      /* 1970 state code 01–56  */                         ///
    byte  stc60  3-4      /* 1960 state code       */                           ///
    int   coc70  5-7      /* 1970 county code      */                           ///
    int   ctabu  8-10     /* county of population  */                           ///
    byte  cencc  11       /* central-county flag   */                           ///
    int   mcd    12-14    /* minor civil division  */                           ///
    int   placc  15-18    /* place code            */                           ///
    byte  platc  19       /* place-type code       */                           ///
    /* strings (SPSS marks with (A)) */                                         ///
    str2  plasc  20-21    /* place-size code       */                           ///
    str1  stcac  22-22    /* consolidated-area code*/                           ///
    str4  smsa   23-26    /* SMSA code             */                           ///
    /* more numeric */                                                          ///
    int   urbca  27-30    /* urbanized-area code   */                           ///
    int   trac   31-34    /* tracted-area code     */                           ///
    byte  uniap  35       /* universal area prefix */                           ///
    int   uniac  36-40    /* universal area code   */                           ///
    /* strings again */                                                         ///
    str2  steac  41-42    /* state economic area   */                           ///
    str3  ecosc  43-45    /* economic sub-region   */                           ///
    str1  cebdc  46-46    /* CBD flag              */                           ///
    str30 arnam  47-76    /* area name             */                           ///
    /* back to numeric (ICPSR dropped Gov. ID; BTC starts here) */              ///
    long  btc    77-80    /* basic tract code      */                           ///
    int   tsc    81-82    /* tract suffix code     */                           ///
    byte  blgc   83       /* block-group code      */                           ///
    str5  endc   84-88    /* enum.-district code   */                           ///
    str1  urrc   89-89    /* urban/rural flag      */                           ///
    int   warc   90-91    /* ward code             */                           ///
    int   codc   92-93    /* congressional district*/                           ///
    long  houc   94-100   /* housing count         */                           ///
    long  popc   101-108  /* population count      */                           ///
    long  sdc    109-113  /* school-district code  */                           ///
    byte  sdtc   114-114  /* school-district type  */                           ///
    int   aduc   115-116  /* admin-unit code       */                           ///
    int   perc   117-119  /* percent equivalent    */                           ///
using "`dfile'", clear


*** Labels
label variable stc70  "1970 State Code"
label variable stc60  "1960 State Code"
label variable coc70  "1970 County Code"
label variable ctabu  "County of Population"
label variable cencc  "Central County Code"
label variable mcd    "Minor Civil Division"
label variable placc  "Place Code"
label variable platc  "Place Type Code"
label variable plasc  "Place Size Code"
label variable stcac  "Std. Consolidated Area Code"
label variable smsa   "SMSA Code"
label variable urbca  "Urbanized Area Code"
label variable trac   "Tracted Area Code"
label variable uniap  "Universal Area Prefix"
label variable uniac  "Universal Area Code"
label variable steac  "State Economic Area Code"
label variable ecosc  "Economic Subregion Code"
label variable cebdc  "Central Business District Code"
label variable arnam  "Area Name"
label variable btc    "Basic Tract Code"
label variable tsc    "Tract Suffix Code"
label variable blgc   "Block Group Code"
label variable endc   "Enumeration District Code"
label variable urrc   "Urban/Rural Class Code"
label variable warc   "Ward Code"
label variable codc   "Congressional District Code"
label variable houc   "Housing Count"
label variable popc   "Population Count"
label variable sdc    "School District Code"
label variable sdtc   "School District Type Code"
label variable aduc   "Administrative Unit Code"


* Clean and construct geographic identifiers
gen no_tract = 0
replace no_tract = 1 if missing(trac) 

*** Basic Tract Code (btc) should be 4 digits; if missing, treat as 0000
*** Tract-Suffix Code (tsc) should be 2 digits; if missing, treat as 00 
replace btc = 0  if missing(btc) & !missing(trac)
replace tsc = 0 if tsc == . & !missing(trac)


* Build composite identifiers
gen str4 btc_str = string(btc,"%04.0f")
gen str2 tsc_str = string(tsc,"%02.0f")
gen str5 sdc_str = string(sdc,"%05.0f")


*** Now build the 11-char tract ID safely
gen str11 tract70 = ///
    string(stc70,"%02.0f") + string(coc70,"%03.0f") + btc_str + tsc_str
gen str13 gisjoin2 = ///
	string(stc70,"%02.0f") + "0" + string(coc70,"%03.0f") + "0" + btc_str + tsc_str 
gen str7 LEAID = string(stc70,"%02.0f") + sdc_str
	
*** Drop the two-digit suffix only when it equals "00"
replace gisjoin2 = substr(gisjoin2, 1, 11) if substr(gisjoin2, -2, 2) == "00"

*** SAVE tract-level GRF IDs with the no_tract flag (keep it!)
preserve
	gen str5 county_code = string(stc70,"%02.0f") + string(coc70,"%03.0f")
    keep LEAID tract70 county_code no_tract 


	
    save grf_id_tractlevel, replace
restore

*** one row per LEAID with its district-type flag (sdtc)
keep LEAID 
duplicates tag LEAID, gen(dup)
bysort LEAID: keep if _n == 1
drop if missing(LEAID)
drop dup
save grf_id, replace


********************************************************************************
* A) HYGIENE
********************************************************************************
cd "$SchoolSpending/data"


use "indfin_panel.dta", clear
assert !missing(GOVID) & strlen(GOVID)==9

********************************************************************************
* B) BASELINE ON INDFIN (GOVID level, pre-dedup; then propagate)
********************************************************************************
gen byte _in_baseline = inlist(year4, 1967, 1970, 1971, 1972)
gen byte _present_baseline = (_in_baseline==1 & !missing(pp_exp))

gen byte _in_baseline_6771 = inlist(year4, 1967, 1970, 1971)
gen byte _present_baseline_6771 = (_in_baseline_6771==1 & !missing(pp_exp))

* Master GOVID list
preserve
    keep GOVID
    duplicates drop
    tempfile govmaster
    save `govmaster', replace
restore

* Collapse 1967–72
preserve
    keep if _in_baseline==1
    collapse (sum) n_baseline_years_present = _present_baseline, by(GOVID)
    tempfile basecnt
    save `basecnt', replace
restore

* Collapse 1967–71
preserve
    keep if _in_baseline_6771==1
    collapse (sum) n_baseline_years_present_6771 = _present_baseline_6771, by(GOVID)
    tempfile basecnt6771
    save `basecnt6771', replace
restore

* Merge baseline counts
use `govmaster', clear
merge 1:1 GOVID using `basecnt'
drop _merge
merge 1:1 GOVID using `basecnt6771'
drop _merge

replace n_baseline_years_present = 0 if missing(n_baseline_years_present)
replace n_baseline_years_present_6771 = 0 if missing(n_baseline_years_present_6771)

gen byte good_govid_baseline = (n_baseline_years_present==4)
gen byte good_govid_baseline_6771 = (n_baseline_years_present_6771==3)

********************************************************************************
* Add per-year completeness (1967, 1970–72)
********************************************************************************
preserve
    use "indfin_panel.dta", clear
    local years 1967 1970 1971 1972
    foreach y of local years {
        bys GOVID: egen byte good_govid_`y' = max(year4==`y' & !missing(pp_exp))
    }
    keep GOVID good_govid_*
    duplicates drop
    tempfile govyeartags
    save `govyeartags', replace
restore

merge 1:1 GOVID using `govyeartags', nogen

label var good_govid_baseline        "Nonmissing spending all 4 baseline years (1967,1970–72)"
label var good_govid_baseline_6771   "Nonmissing spending all 3 baseline years (1967,1970–71)"
label var good_govid_1970            "Nonmissing spending in 1970"

tempfile govtag
save `govtag', replace

********************************************************************************
* Propagate tags to INDFIN rows
********************************************************************************
use "indfin_panel.dta", clear
merge m:1 GOVID using `govtag', nogen

isid GOVID year4
gen byte has_indfin = 1
tempfile indfin_work
save `indfin_work', replace


********************************************************************************
* C) STRICT 1:1 LEAID↔GOVID MAP FROM F-33 (non-junk only)
********************************************************************************

use "f33_id.dta", clear
// Reduce to unique LEAID–GOVID pairs across all years
keep LEAID GOVID 
bysort LEAID GOVID: keep if _n==1

keep LEAID GOVID
    drop if missing(LEAID) 
	drop if LEAID == "M"
	drop if LEAID == "N"
    bysort LEAID: gen n_govid = _N       // distinct GOVIDs for this LEAID
    bysort GOVID: gen n_leaid = _N       // distinct LEAIDs for this GOVID
    gen byte rel_type = .
    replace rel_type = 1 if n_govid==1 & n_leaid==1
    replace rel_type = 2 if n_govid>1  & n_leaid==1    // 1:M (LEAID→GOVID)
    replace rel_type = 3 if n_govid==1 & n_leaid>1     // 1:M (GOVID→LEAID)
    replace rel_type = 4 if n_govid>1  & n_leaid>1     // M:M
    label define rel 1 "1:1" 2 "1:M (LEAID→GOVID)" 3 "1:M (GOVID→LEAID)" 4 "M:M"
    label values rel_type rel
keep if rel_type == 1 

isid LEAID  // should be unique now



tempfile map1to1
save `map1to1', replace
save "f33_1to1_map.dta", replace

********************************************************************************
* D) MAP INDFIN → LEAID VIA 1:1 MAP; CAPTURE UNMAPPED
********************************************************************************
use `govtag', clear
merge m:1 GOVID using `map1to1'

gen byte mapped_1to1   = !missing(LEAID)
gen byte fail_unmapped = (mapped_1to1==0)
label var mapped_1to1   "INDFIN GOVID mapped to LEAID via F-33 1:1"
label var fail_unmapped "GOVID had no 1:1 LEAID map (excluded from main panel)"

drop if _merge == 1 // Dropping GOVIDs that could never effect tracts' goodness
replace good_govid_baseline = 0 if missing(good_govid_baseline) & _merge == 2
assert !missing(LEAID)

// Post-join uniqueness at LEAID×year4 (must pass)
isid LEAID 
drop _merge

// Order and label
order LEAID GOVID good_govid_baseline ///
      n_baseline_years_present
********************************************************************************
* E) ATTACH has_f33 PRESENCE AND FINAL TAGS
********************************************************************************

merge 1:m GOVID using "indfin_panel.dta"
drop if _merge ==2
drop _merge
replace good_govid_baseline = 0 if missing(good_govid_baseline)
drop rel_type
save "indfin_panel_tagged.dta", replace


********************************************************************************
* F) ATTACH has_f33 PRESENCE AND FINAL TAGS
********************************************************************************

use "f33_panel.dta", clear
// Reduce to unique LEAID–GOVID pairs across all years
bysort LEAID GOVID: keep if _n==1
preserve
keep LEAID GOVID
    drop if missing(LEAID) 
	drop if LEAID == "M"
	drop if LEAID == "N"
    bysort LEAID: gen n_govid = _N       // distinct GOVIDs for this LEAID
    bysort GOVID: gen n_leaid = _N       // distinct LEAIDs for this GOVID
    gen byte rel_type = .
    replace rel_type = 1 if n_govid==1 & n_leaid==1
    replace rel_type = 2 if n_govid>1  & n_leaid==1    // 1:M (LEAID→GOVID)
    replace rel_type = 3 if n_govid==1 & n_leaid>1     // 1:M (GOVID→LEAID)
    replace rel_type = 4 if n_govid>1  & n_leaid>1     // M:M
    label define rel 1 "1:1" 2 "1:M (LEAID→GOVID)" 3 "1:M (GOVID→LEAID)" 4 "M:M"
    label values rel_type rel
keep if rel_type == 1
drop rel_type
save f33_11,replace
restore
use f33_11, clear
merge 1:m LEAID GOVID using f33_panel
keep if _merge ==3
drop if missing(LEAID) 
	drop if LEAID == "M"
	drop if LEAID == "N"

rename year year4
append using "indfin_panel_tagged.dta"
keep LEAID GOVID year4 pp_exp good_govid_baseline ///
good_govid_1967 good_govid_1970 good_govid_1971 good_govid_1972 good_govid_baseline_6771
duplicates drop LEAID GOVID year4 pp_exp, force
bysort LEAID: egen __g = max(good_govid_baseline)
replace good_govid_baseline = __g if missing(good_govid_baseline)
drop __g

save "district_panel_tagged.dta", replace
use "district_panel_tagged.dta", clear
drop if missing(good_govid_baseline)
collapse (mean) pp_exp, by(year4)
twoway line pp_exp year4

********************************************************************************
* G) FINAL CHECK: open and show a sample (non-interactive)
********************************************************************************
use "district_panel_tagged.dta", clear





********************************************************************************
* GRF) Merege GRF -> Tagged District Panel
********************************************************************************

use grf_id,clear
merge 1:m LEAID using "district_panel_tagged.dta"
keep if _merge ==3


replace year4 = 9999 if missing(year4)
isid LEAID year4
drop _merge
save f33_indfin_grf_canon, replace

